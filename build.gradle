plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'java'
}

group = 'de.prkz.twitch'
version = '1.1-SNAPSHOT'
description = 'Twitch Chat Analyser'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext.mainClassName = 'de.prkz.twitch.emoteanalyser.EmoteAnalyser'

def flinkVersion = '1.13.2'
def flinkScalaVersion = '2.12'

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io'}
}

dependencies {
	implementation "org.apache.flink:flink-connector-kafka_${flinkScalaVersion}:${flinkVersion}"
	implementation "org.apache.flink:flink-metrics-dropwizard:${flinkVersion}"
	implementation "com.github.pircbotx:pircbotx:2.2"

	implementation 'com.github.twitch4j:twitch4j:1.5.0'
	// Explicitly specifying this here is required because transitive dependency from twitch4j is broken in 1.5.0:
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.3'

	implementation 'org.postgresql:postgresql:42.2.23'
	implementation 'org.json:json:20210307'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'

	compileOnly "org.apache.flink:flink-java:${flinkVersion}"
	compileOnly "org.apache.flink:flink-core:${flinkVersion}"
	compileOnly "org.apache.flink:flink-streaming-java_${flinkScalaVersion}:${flinkVersion}"
	compileOnly "org.apache.flink:flink-clients_${flinkScalaVersion}:${flinkVersion}"
}

shadowJar {
	mergeServiceFiles()

	manifest {
		attributes 'Main-Class': "$mainClassName"
	}
}

tasks.build.dependsOn tasks.shadowJar
