plugins {
	id 'java-library'
}

group = 'de.prkz.twitch'
version = '1.1-SNAPSHOT'
description = 'Twitch Chat Analyser'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext.mainClassName = 'de.prkz.twitch.emoteanalyser.EmoteAnalyser'

def flinkVersion = '1.13.2'
def flinkScalaVersion = '2.12'

if (!hasProperty('mainClass')) {
	ext.mainClass = "$mainClassName"
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io'}
}

dependencies {
	implementation "org.apache.flink:flink-connector-kafka_${flinkScalaVersion}:${flinkVersion}"
	implementation "org.apache.flink:flink-metrics-dropwizard:${flinkVersion}"
	implementation "com.github.pircbotx:pircbotx:2.2"
	implementation 'com.github.twitch4j:twitch4j:1.2.1'
	implementation 'org.postgresql:postgresql:42.2.14'
	implementation 'org.json:json:20180130'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'

	compileOnly "org.apache.flink:flink-java:${flinkVersion}"
	compileOnly "org.apache.flink:flink-core:${flinkVersion}"
	compileOnly "org.apache.flink:flink-streaming-java_${flinkScalaVersion}:${flinkVersion}"
	compileOnly "org.apache.flink:flink-clients_${flinkScalaVersion}:${flinkVersion}"
}

jar {
	manifest {
		attributes(
				//'Class-Path': configurations.implementation.collect { it.getName() }.join(' '),
				'Main-Class': "$mainClassName",
				'Implementation-Title': project.name,
				'Implementation-Version': project.version
		)
	}

	from {
		configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
		//configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
